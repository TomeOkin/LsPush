allprojects {
    repositories {
        jcenter()
    }
}

ext {
    File file = project.rootProject.file('local.properties')
    Properties properties = new Properties()
    boolean isExists = file.exists();
    if (file.exists()) {
        properties.load(file.newDataInputStream())
    }
    metadata = [
            alias           : isExists ? properties.getProperty("key.alias") : System.getenv("ALIAS_NAME"),
            pwd             : isExists ? properties.getProperty("key.pwd") : System.getenv("ALIAS_PWD"),
            storeFile       : isExists ? properties.getProperty("key.storeFile") : System.getenv("STORE_FILE"),
            storePwd        : isExists ? properties.getProperty("key.storePwd") : System.getenv("STORE_PWD"),
            lspushServerUrl : isExists ? properties.getProperty("lspush.serverUrl") : System.getenv("LSPUSH_SERVER_URL"),
            lspushPublicKey : isExists ? properties.getProperty("lspush.publicKey") : System.getenv("LSPUSH_PUBLIC_KEY"),
            buglyAppId      : isExists ? properties.getProperty("bugly.appId") : System.getenv("BUGLY_APPID"),
            buglyAppKey     : isExists ? properties.getProperty("bugly.appKey") : System.getenv("BUGLY_APPKEY"),
            mobSMSId        : isExists ? properties.getProperty("mob.sms.id") : System.getenv("MOB_SMS_ID"),
            mobSMSKey       : isExists ? properties.getProperty("mob.sms.key") : System.getenv("MOB_SMS_KEY"),
            buglyAppVersion : "1.0",
            buglyEnableDebug: false
    ]

    releaseDependencies = [
            // support-library https://developer.android.com/topic/libraries/support-library/revisions.html
            supportV7            : "com.android.support:appcompat-v7:24.2.0",
            supportV4            : "com.android.support:support-v4:24.2.0",
            supportAnnotations   : "com.android.support:support-annotations:24.2.0",
            recyclerview         : "com.android.support:recyclerview-v7:24.2.0",
            cardview             : "com.android.support:cardview-v7:24.2.0",
            design               : "com.android.support:design:24.2.0",

            // log
            timber               : "com.jakewharton.timber:timber:4.3.0",
            logger               : "com.orhanobut:logger:1.15",

            // rx
            rxandroid            : "io.reactivex:rxandroid:1.2.1",
            rxjava               : "io.reactivex:rxjava:1.1.10",

            rxbindingV7          : "com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0",
            rxbindingRecyclerview: "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0",

            // inject
            butterknife          : "com.jakewharton:butterknife:8.4.0",
            butterknifeCompiler  : "com.jakewharton:butterknife-compiler:8.4.0",

            // dagger
            dagger2              : "com.google.dagger:dagger:2.6.1",
            dagger2Compiler      : "com.google.dagger:dagger-compiler:2.6.1",
            javaxAnnotation      : "org.glassfish:javax.annotation:10.0-b28", // provided; after dagger 2.1, don't need it

            // network
            retrofit2            : "com.squareup.retrofit2:retrofit:2.1.0",
            okhttp3              : "com.squareup.okhttp3:okhttp:3.4.1",
            retrofit2Gson        : "com.squareup.retrofit2:converter-gson:2.1.0",
            // stetho has problems in some device, so also using this
            okhttp3Logging       : "com.squareup.okhttp3:logging-interceptor:3.4.1",

            // gson
            gson                 : "com.google.code.gson:gson:2.7",

            // image
            glide                : "com.github.bumptech.glide:glide:3.7.0",
            glideOkhttp3          : "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar",

            // eventbus
            eventbus3            : "org.greenrobot:eventbus:3.0.0",

            // crash report
            // bugly, https://bugly.qq.com/androidfast
            crashreport          : "com.tencent.bugly:crashreport:2.2.2",
            nativecrashreport    : "com.tencent.bugly:nativecrashreport:3.0"
    ]

    debugDependencies = [
            stetho       : "com.facebook.stetho:stetho:1.4.0",
            stethoOkhttp3: "com.facebook.stetho:stetho-okhttp3:1.4.0",
            stethoJsRhino: "com.facebook.stetho:stetho-js-rhino:1.4.0",
    ]

    testDependencies = [
            junit      : "junit:junit:4.12",
            robolectric: "org.robolectric:robolectric:3.1.2",
            assertjCore: "org.assertj:assertj-core:3.5.2",
            mockitoCore: "org.mockito:mockito-core:1.10.19"
    ]

    androidTestDependencies = [
            junit              : "junit:junit:4.12",
            mockitoCore        : "org.mockito:mockito-core:1.10.19",
            assertjAppCompatV7 : "com.squareup.assertj:assertj-android-appcompat-v7:1.1.1@aar",
            assertjRecyclerView: "com.squareup.assertj:assertj-android-recyclerview-v7:1.1.1@aar",
            espresso           : "com.android.support.test.espresso:espresso-core:2.2.2",
            runner             : "com.android.support.test:runner:0.5",
            supportAnnotations : "com.android.support:support-annotations:24.2.0"
    ]
}