allprojects {
    repositories {
        jcenter()
    }
}

ext {
    supportVersion = "25.0.0"
    rxbindingVersion = "0.4.0"
    butterknifeVersion = "8.4.0"
    daggerVersion = "2.7"
    retrofit2Version = "2.1.0"
    okhttp3Version = "3.4.2"
    stethoVersion = "1.4.1"
    junit4Version = "4.12"
    mockitoVersion = "1.10.19"
    mockito2Version = "2.2.15"

    File file = project.rootProject.file('local.properties')
    Properties properties = new Properties()
    boolean isExists = file.exists();
    if (file.exists()) {
        properties.load(file.newDataInputStream())
    }
    metadata = [
            alias           : isExists ? properties.getProperty("key.alias") : System.getenv("ALIAS_NAME"),
            pwd             : isExists ? properties.getProperty("key.pwd") : System.getenv("ALIAS_PWD"),
            storeFile       : isExists ? properties.getProperty("key.storeFile") : System.getenv("STORE_FILE"),
            storePwd        : isExists ? properties.getProperty("key.storePwd") : System.getenv("STORE_PWD"),

            lspushServerUrl : isExists ? properties.getProperty("lspush.serverUrl") : System.getenv("LSPUSH_SERVER_URL"),
            lspushPublicKey : isExists ? properties.getProperty("lspush.publicKey") : System.getenv("LSPUSH_PUBLIC_KEY"),

            buglyAppVersion : "1.0",
            buglyEnableDebug: false,
            buglyAppId      : isExists ? properties.getProperty("bugly.appId") : System.getenv("BUGLY_APPID"),
            buglyAppKey     : isExists ? properties.getProperty("bugly.appKey") : System.getenv("BUGLY_APPKEY"),

            mobSMSId        : isExists ? properties.getProperty("mob.sms.id") : System.getenv("MOB_SMS_ID"),
            mobSMSKey       : isExists ? properties.getProperty("mob.sms.key") : System.getenv("MOB_SMS_KEY"),

            jaqKey          : isExists ? properties.getProperty("jaq.key") : System.getenv("JAQ_KEY")
    ]

    releaseDependencies = [
            // support-library https://developer.android.com/topic/libraries/support-library/revisions.html
            supportV13           : "com.android.support:support-v13:${supportVersion}",
            supportV7            : "com.android.support:appcompat-v7:${supportVersion}",
            supportV4            : "com.android.support:support-v4:${supportVersion}",
            supportAnnotations   : "com.android.support:support-annotations:${supportVersion}",
            recyclerview         : "com.android.support:recyclerview-v7:${supportVersion}",
            cardview             : "com.android.support:cardview-v7:${supportVersion}",
            design               : "com.android.support:design:${supportVersion}",
            palette              : "com.android.support:palette-v7:${supportVersion}",

            // log
            timber               : "com.jakewharton.timber:timber:4.3.1",
            logger               : "com.orhanobut:logger:1.15",

            // rx
            rxandroid            : "io.reactivex:rxandroid:1.2.1",
            rxjava               : "io.reactivex:rxjava:1.2.2",

            rxbindingV7          : "com.jakewharton.rxbinding:rxbinding-appcompat-v7:${rxbindingVersion}",
            rxbindingRecyclerview: "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:${rxbindingVersion}",

            // inject
            butterknife          : "com.jakewharton:butterknife:${butterknifeVersion}",
            butterknifeCompiler  : "com.jakewharton:butterknife-compiler:${butterknifeVersion}",

            // dagger
            dagger2              : "com.google.dagger:dagger:${daggerVersion}",
            dagger2Compiler      : "com.google.dagger:dagger-compiler:${daggerVersion}",
            javaxAnnotation      : "org.glassfish:javax.annotation:10.0-b28", // provided; after dagger 2.1, don't need it

            // network
            retrofit2            : "com.squareup.retrofit2:retrofit:${retrofit2Version}",
            okhttp3              : "com.squareup.okhttp3:okhttp:${okhttp3Version}",
            retrofit2Gson        : "com.squareup.retrofit2:converter-gson:${retrofit2Version}",
            retrofit2Rxjava      : "com.squareup.retrofit2:adapter-rxjava:${retrofit2Version}",
            // stetho has problems in some device, so also using this
            okhttp3Logging       : "com.squareup.okhttp3:logging-interceptor:${okhttp3Version}",

            // gson
            gson                 : "com.google.code.gson:gson:2.8.0",

            // image
            glide                : "com.github.bumptech.glide:glide:3.7.0",
            glideOkhttp3         : "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar",

            // eventbus
            eventbus3            : "org.greenrobot:eventbus:3.0.0",

            // crash report
            // bugly, https://bugly.qq.com/androidfast
            crashreport          : "com.tencent.bugly:crashreport:2.3.1",
            nativecrashreport    : "com.tencent.bugly:nativecrashreport:3.0"
    ]

    debugDependencies = [
            stetho       : "com.facebook.stetho:stetho:${stethoVersion}",
            stethoOkhttp3: "com.facebook.stetho:stetho-okhttp3:${stethoVersion}",
            stethoJsRhino: "com.facebook.stetho:stetho-js-rhino:${stethoVersion}",
    ]

    testDependencies = [
            junit       : "junit:junit:${junit4Version}",
            robolectric : "org.robolectric:robolectric:3.1.4",
            assertjCore : "org.assertj:assertj-core:3.5.2",
            mockitoCore : "org.mockito:mockito-core:${mockitoVersion}",
            mockitoCore2: "org.mockito:mockito-core:${mockito2Version}",
    ]

    androidTestDependencies = [
            junit              : "junit:junit:${junit4Version}",
            mockitoCore        : "org.mockito:mockito-core:${mockitoVersion}",
            mockitoCore2       : "org.mockito:mockito-core:${mockito2Version}",
            assertjAppCompatV7 : "com.squareup.assertj:assertj-android-appcompat-v7:1.1.1@aar",
            assertjRecyclerView: "com.squareup.assertj:assertj-android-recyclerview-v7:1.1.1@aar",
            espresso           : "com.android.support.test.espresso:espresso-core:2.2.2",
            runner             : "com.android.support.test:runner:0.5",
            supportAnnotations : "com.android.support:support-annotations:${supportVersion}"
    ]
}